# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.gcc5
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (built with GCC 5)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'gcc5')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    # Disable flags unknown to GCC 5
    newcxxflags=$(echo $CXXFLAGS | sed s/-fstack-protector-strong.//g)
    export CXXFLAGS="$newcxxflags"

    if [ ! -d "$_rootdir/build-gcc5" ] ; then
        mkdir "$_rootdir/build-gcc5"
        cd "$_rootdir/build-gcc5"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-5 \
            -G Ninja
    fi

    cd "$_rootdir/build-gcc5"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-gcc5"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-gcc5"
    DESTDIR="$pkgdir/" ninja install
}

# kate: hl bash
