# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.clang.libc++
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (built with Clang and libc++)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'clang' 'libc++')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-clang-libc++" ] ; then
        mkdir "$_rootdir/build-clang-libc++"
        cd "$_rootdir/build-clang-libc++"

        cmake .. \
            -DCMAKE_CXX_COMPILER=clang++ \
            -DCMAKE_CXX_FLAGS="-stdlib=libc++" \
            -DCMAKE_EXE_LINKER_FLAGS=-lc++abi \
            -G Ninja
    fi

    cd "$_rootdir/build-clang-libc++"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    export CORRADE_TEST_COLOR=ON

    cd "$_rootdir/build-clang-libc++"
    # The Tester test *needs* benchmarks enabled to pass properly
    ctest --output-on-failure -R TestSuiteTesterTest
    CORRADE_TEST_SKIP_BENCHMARKS=ON ctest --output-on-failure -j5 -E TestSuiteTesterTest
}

package() {
    cd "$_rootdir/build-clang-libc++"
    DESTDIR="$pkgdir/" ninja install
}

# kate: hl bash
