# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.clang.threadsanitizer
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (clang-threadsanitizer build)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'clang')
options=(!strip)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-clang-threadsanitizer"
    cd "$_rootdir/build-clang-threadsanitizer"

    cmake .. \
        -DCMAKE_CXX_COMPILER=clang++ \
        -DCMAKE_CXX_FLAGS="-fsanitize=thread" \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON \
        -G Ninja
    ninja
}

check() {
    cd "$_rootdir/build-clang-threadsanitizer"
    TSAN_OPTIONS="color=always" CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-clang-threadsanitizer"
    DESTDIR="$pkgdir/" ninja install
}

# kate: hl bash
