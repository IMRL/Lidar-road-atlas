# Author: mosra <mosra@centrum.cz>
pkgname=mingw-w64-corrade
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (mingw-w64)"
arch=('any')
url="https://magnum.graphics/corrade/"
license=('MIT')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'cmake' 'ninja' 'corrade')
options=(!buildflags !strip)

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-mingw-w64-32"
    cd "$_rootdir/build-mingw-w64-32"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE=../toolchains/archlinux/basic-mingw-w64-32.cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/i686-w64-mingw32 \
        -DBUILD_TESTS=ON \
        -G Ninja
    ninja

    mkdir -p "$_rootdir/build-mingw-w64-64"
    cd "$_rootdir/build-mingw-w64-64"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE=../toolchains/archlinux/basic-mingw-w64-64.cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/x86_64-w64-mingw32 \
        -DBUILD_TESTS=ON \
        -G Ninja
    ninja
}

package() {
    cd "$_rootdir/build-mingw-w64-32"
    DESTDIR="$pkgdir/" ninja install/strip

    cd "$_rootdir/build-mingw-w64-64"
    DESTDIR="$pkgdir/" ninja install/strip
}

# kate: hl bash
