# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.release
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (debug+release libs)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja')
options=('!strip')
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build"
    cd "$_rootdir/build"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=TRUE \
        -G Ninja
    ninja

    mkdir -p "$_rootdir/build-release"
    cd "$_rootdir/build-release"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=TRUE \
        -G Ninja
    ninja
}

check() {
    export CORRADE_TEST_COLOR=ON

    cd "$_rootdir/build"
    # The Tester test *needs* benchmarks enabled to pass properly
    ctest --output-on-failure -R TestSuiteTesterTest
    CORRADE_TEST_SKIP_BENCHMARKS=ON ctest --output-on-failure -j5 -E TestSuiteTesterTest

    cd "$_rootdir/build-release"
    # The Tester test *needs* benchmarks enabled to pass properly
    ctest --output-on-failure -R TestSuiteTesterTest
    CORRADE_TEST_SKIP_BENCHMARKS=ON ctest --output-on-failure -j5 -E TestSuiteTesterTest
}

package() {
    cd "$_rootdir/build"
    DESTDIR="$pkgdir/" ninja install

    cd "$_rootdir/build-release"
    DESTDIR="$pkgdir/" ninja install/strip
}

# kate: hl bash
