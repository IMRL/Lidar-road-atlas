# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-corrade
pkgver=dev.wasm
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (Emscripten, wasm)"
arch=('any')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'emscripten' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-wasm" ] ; then
        mkdir "$_rootdir/build-emscripten-wasm"
        cd "$_rootdir/build-emscripten-wasm"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten-wasm.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-wasm"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DBUILD_TESTS=OFF
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-wasm"

    # So tests are not even built (takes AGES) when the user doesn't want to
    cmake . -DBUILD_TESTS=ON
    ninja

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-wasm"
    DESTDIR="$pkgdir/" ninja install
}

# kate: hl bash
